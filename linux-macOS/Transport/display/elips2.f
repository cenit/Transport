      SUBROUTINE ELIPS2(NNR,NRJ,SCALX,SCALY,ISIZE,COLF)
C
C   PLOTS BEAM ELLIPSES
C   ADAPTED FOR POSTSCRIPT BY U.ROHRER (PSI), MAY 1990
C   For longitudinal phase space by U. Rohrer (PSI), Apr. 2005
C
      COMMON /COLTAG/ COLOR, XSC, YSC, XO, YO, CHSIZE
      LOGICAL         COLOR
C
      PARAMETER   (LDM=2000)
      CHARACTER*2 DIM1, DIM3, DIM7
      CHARACTER*4 LAB(LDM)
      DIMENSION   Z(LDM),DLMAX(LDM),DPPMAX(LDM),R56(LDM)
C
      COMMON /EP/ Z,DLMAX,DPPMAX,R56,LAB,DIM1,DIM3,DIM7
C
      CHARACTER   TEXT*80
      DIMENSION   SCAL(4),FSCAL(4),OFFX(4),OFFY(4),NRJ(4)
      EQUIVALENCE(SCAL(1),XSCAL), (SCAL(2),YSCAL),
     1           (SCAL(3),PXSCAL),(SCAL(4),PYSCAL)
      REAL        X1(101),X2(101),Y1(101),Y2(101)
      REAL        LWX,LWY
      LOGICAL     COLF
C
C     DATA        OFFX /250.,1270.,250.,1270./
      DATA        OFFX /250.,760.,250.,760./
      DATA        OFFY /1100.,1100.,325.,325./
      DATA        PI   /3.14159/
C
      COLOR = COLF
      XSC = 0.64
      YSC = 0.63
      XO  = 70.0
      YO  = 40.0
      CHSIZE = 11.0
      CALL PSOPEN('TRANS.PS')
      CALL PSLINW(2.*SCALX)
      SIZE = 7.
      IF (ISIZE .EQ. 3) SIZE = 15.
      CALL PSTXTS(SIZE)
C
      IF (DIM1 .EQ. 'M ') DIM1 = 'm '
      IF (DIM7 .EQ. 'MM') DIM7 = 'mm'
      IF (DIM7 .EQ. 'CM') DIM7 = 'cm'
C
C   DISPLAY 4 ELLIPSE PAIRES
C
 6    DO 97 NJ = 1, NNR
      JJJ = NRJ(NJ)
      IF (JJJ .EQ. 0) GO TO 97
      J = JJJ
C
C     AUTOSCALING
C
      M=DLMAX(J)*10.+1.001
      XSCAL=FLOAT(M)/10.+.00001
      IF(XSCAL.LT.0.1) XSCAL=.1
      M=DPPMAX(J)*10.+1.001
      PXSCAL=FLOAT(M)/10.+.00001
      IF(PXSCAL.LT.0.1) PXSCAL=.1

      CH1=ASIN(R56(J))
      X1M=200.*DLMAX(J)/XSCAL
      Y1M=200.*DPPMAX(J)/PXSCAL
C
C   BERECHNE SKALENABSCHNITTE
C
      DO 110 I=1,4
       X=SCAL(I)
       DO 99 JX=1,10
        IF(X.LT.1.0) X=X*10.
        IF(X.GT.10.0) X=X/10.
 99   CONTINUE
      ABSCH=200./X
      IIX=X
      IF(IIX.EQ.1) ABSCH=ABSCH/5.
      IF(IIX.EQ.2) ABSCH=ABSCH/2.
      IF(IIX.EQ.3) ABSCH=ABSCH/2.
      FSCAL(I)=ABSCH
 110  CONTINUE
C
C   KONSTRUIERE ELLIPSEN
C
      DO 10 I=1,101
       DE=2.*(I-1)*PI/100.
       COSD=COS(DE)
       X1(I)=X1M*COSD
       Y1(I)=Y1M*SIN(DE+CH1)
 10   CONTINUE
C
C   EMITTANZ
C
      EX=DLMAX(J)*DPPMAX(J)*SQRT(1.-R56(J)**2)
C
C   WINKEL CHIX & CHIY
C
      CHIX=180./PI*ASIN(R56(J))
C
C   ZEICHNE ELLIPSEN
C
      OFFX1 = OFFX(NJ)
      OFFY1 = OFFY(NJ)
       IF (NNR .EQ. 1) THEN
        OFFX1 = OFFX(3)
        OFFY1 = OFFY(3)
       ENDIF
      OFFX2 = OFFX1+500.
      BEGX1 = OFFX1-200.
      ENDX1 = OFFX1+200.
      BEGX2 = OFFX2-200.
      ENDX2 = OFFX2+200.
      BEGY1 = OFFY1-200.
      ENDY1 = OFFY1+200.
      CALL SETCOL(3) ! GRUEN
      CALL PSMOV((OFFX1+X1(1))*SCALX,(OFFY1+Y1(1))*SCALY)
      DO 15 I=2,101
      CALL PSDRW((OFFX1+X1(I))*SCALX,(OFFY1+Y1(I))*SCALY)
 15   CONTINUE
C
C   KONJUGIERTE DURCHMESSER
C
      XK1=R56(J)*X1M
      XK2=R56(J)*Y1M
      XK3=R56(J)*X2M
      XK4=R56(J)*Y2M
      XL1=X1M
      XL2=Y1M
      XL3=X2M
      XL4=Y2M
      CALL PSMOV((OFFX1+XK1)*SCALX,(OFFY1+XL2)*SCALY)
      CALL PSDRW((OFFX1-XK1)*SCALX,(OFFY1-XL2)*SCALY)
      CALL PSMOV((OFFX1+XL1)*SCALX,(OFFY1+XK2)*SCALY)
      CALL PSDRW((OFFX1-XL1)*SCALX,(OFFY1-XK2)*SCALY)
C
C   ZEICHNE ACHSEN
C
      CALL SETCOL(2) ! ROT
      CALL PSMOV(BEGX1*SCALX,OFFY1*SCALY)
      CALL PSDRW(ENDX1*SCALX,OFFY1*SCALY)
      CALL PSMOV(OFFX1*SCALX,BEGY1*SCALY)
      CALL PSDRW(OFFX1*SCALX,ENDY1*SCALY)
C
C   ZEICHNE ACHSEN-ABSCHNITTE
C
      DO 300 K=1,10
      XX1=(OFFX1+FSCAL(1)*FLOAT(K))*SCALX
      XX2=(OFFX1-FSCAL(1)*FLOAT(K))*SCALX
      IF(XX1.GT.ENDX1*SCALX) GO TO 300
      IF(XX2.LT.BEGX1*SCALX) GO TO 300
      CALL PSMOV(XX1,(OFFY1-3.)*SCALY)
      CALL PSDRW(XX1,(OFFY1+3.)*SCALY)
      CALL PSMOV(XX2,(OFFY1-3.)*SCALY)
      CALL PSDRW(XX2,(OFFY1+3.)*SCALY)
 300  CONTINUE
C
      DO 302 K=1,10
      YY1=(OFFY1+FSCAL(3)*FLOAT(K))*SCALY
      YY2=(OFFY1-FSCAL(3)*FLOAT(K))*SCALY
      IF(YY1.GT.ENDY1*SCALY) GO TO 302
      IF(YY2.LT.BEGY1*SCALY) GO TO 302
      CALL PSMOV((OFFX1-3.)*SCALX,YY1)
      CALL PSDRW((OFFX1+3.)*SCALX,YY1)
      CALL PSMOV((OFFX1-3.)*SCALX,YY2)
      CALL PSDRW((OFFX1+3.)*SCALX,YY2)
 302  CONTINUE
C
C   BESCHRIFTE ACHSEN
C
      CALL PSTEXT((OFFX1+5.)*SCALX,(ENDY1+10.)*SCALY,'dp/p')
      CALL PSTEXT((ENDX1+5.)*SCALX,OFFY1*SCALY,'dl')
C
C   ZEICHNE PARAMETER
C
      CALL SETCOL(1) ! WEISS

      WRITE(TEXT,1006) CHIX, R56(J)
 1006 FORMAT('Chi=',F7.1,' Deg',' (',F7.4,')')
      CALL PSTEXT(BEGX1*SCALX,(ENDY1+85.)*SCALY,TEXT)

      WRITE(TEXT,1008) EX, DIM7, DIM3
 1008 FORMAT('Emit=',F6.2,' * Pi ',A2,A2)
      CALL PSTEXT(BEGX1*SCALX,(ENDY1+50.)*SCALY,TEXT)

      WRITE(TEXT,1013) XSCAL,DIM7,PXSCAL,DIM3
 1013 FORMAT('dlmax=',F5.1,1X,A2,'  (dp/p)max=',F5.1,1X,A2)
      CALL PSTEXT(BEGX1*SCALX,(BEGY1-30.)*SCALY,TEXT)

      WRITE(TEXT,1010) Z(J),DIM1,LAB(J)
 1010 FORMAT('z= ',F7.3,1X,A2,2X,A4)
      CALL PSTEXT(BEGX1*SCALX,(BEGY1-100.)*SCALY,TEXT)
C
 97   CONTINUE
      CALL PSCLS
      RETURN
      END
