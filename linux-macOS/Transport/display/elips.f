      SUBROUTINE ELIPS(NNR,NRJ,SCALX,SCALY,ISIZE,COLF)
C
C   PLOTS BEAM ELLIPSES
C   ADAPTED FOR POSTSCRIPT BY U.ROHRER (PSI), MAY 1990
C
      COMMON /COLTAG/ COLOR, XSC, YSC, XO, YO, CHSIZE
      LOGICAL         COLOR
C
      PARAMETER   (LDM=2000)
      CHARACTER*2 DIM1, DIM2
      CHARACTER*4 LAB(LDM)
      DIMENSION   Z(LDM),XMAX(LDM),YMAX(LDM),D(LDM),DP(LDM)
      DIMENSION   PXMAX(LDM),PYMAX(LDM),R12(LDM),R34(LDM)
      COMMON /EP/ Z,XMAX,YMAX,PXMAX,PYMAX,R12,R34,D,DP,LAB,DIM1,DIM2
C
      CHARACTER   TEXT*80
      DIMENSION   SCAL(4),FSCAL(4),OFFX(4),OFFY(4),NRJ(4)
      EQUIVALENCE(SCAL(1),XSCAL), (SCAL(2),YSCAL),
     1           (SCAL(3),PXSCAL),(SCAL(4),PYSCAL)
      REAL        X1(101),X2(101),Y1(101),Y2(101)
      REAL        LWX,LWY
      LOGICAL     COLF
C
      DATA        OFFX /250.,1270.,250.,1270./
      DATA        OFFY /1100.,1100.,325.,325./
      DATA        PI   /3.14159/
C
      COLOR = COLF
      XSC = 0.64
      YSC = 0.63
      XO  = 70.0
      YO  = 40.0
      CHSIZE = 11.0
      CALL PSOPEN('TRANS.PS')
      CALL PSLINW(2.*SCALX)
      SIZE = 7.
      IF (ISIZE .EQ. 3) SIZE = 15.
      CALL PSTXTS(SIZE)
C
      IF (DIM1 .EQ. 'M ') DIM1 = 'm '
      IF (DIM2 .EQ. 'MM') DIM2 = 'mm'
      IF (DIM2 .EQ. 'CM') DIM2 = 'cm'
C
C   DISPLAY 4 ELLIPSE PAIRES
C
 6    DO 97 NJ = 1, NNR
      JJJ = NRJ(NJ)
      IF (JJJ .EQ. 0) GO TO 97
      J = JJJ
C
C     AUTOSCALING
C
      M=XMAX(J)*10.+1.001
      XSCAL=FLOAT(M)/10.+.00001
      IF(XSCAL.LT.0.1) XSCAL=.1
      M=YMAX(J)*10.+1.001
      YSCAL=FLOAT(M)/10.+.00001
      IF(YSCAL.LT.0.1) YSCAL=.1
      M=PXMAX(J)*10.+1.001
      PXSCAL=FLOAT(M)/10.+.00001
      IF(PXSCAL.LT.0.1) PXSCAL=.1
      M=PYMAX(J)*10.+1.001
      PYSCAL=FLOAT(M)/10.+.00001
      IF(PYSCAL.LT.0.1) PYSCAL=.1
      CH1=ASIN(R12(J))
      CH2=ASIN(R34(J))
      X1M=200.*XMAX(J)/XSCAL
      X2M=200.*YMAX(J)/YSCAL
      Y1M=200.*PXMAX(J)/PXSCAL
      Y2M=200.*PYMAX(J)/PYSCAL
C
C   BERECHNE SKALENABSCHNITTE
C
      DO 110 I=1,4
      X=SCAL(I)
      DO 99 JX=1,10
      IF(X.LT.1.0) X=X*10.
      IF(X.GT.10.0) X=X/10.
 99   CONTINUE
      ABSCH=200./X
      IIX=X
      IF(IIX.EQ.1) ABSCH=ABSCH/5.
      IF(IIX.EQ.2) ABSCH=ABSCH/2.
      IF(IIX.EQ.3) ABSCH=ABSCH/2.
      FSCAL(I)=ABSCH
 110  CONTINUE
C
C   KONSTRUIERE ELLIPSEN
C
      DO 10 I=1,101
      DE=2.*(I-1)*PI/100.
      COSD=COS(DE)
      X1(I)=X1M*COSD
      Y1(I)=Y1M*SIN(DE+CH1)
      X2(I)=X2M*COSD
      Y2(I)=Y2M*SIN(DE+CH2)
 10   CONTINUE
C
C   EMITTANZ
C
      EX=XMAX(J)*PXMAX(J)*SQRT(1.-R12(J)**2)
      EY=YMAX(J)*PYMAX(J)*SQRT(1.-R34(J)**2)
C
C   WAIST DISTANZ
C
      IF(PXMAX(J).EQ.0.0) GO TO 13
      LWX=-R12(J)*XMAX(J)/PXMAX(J)
 13   IF(PXMAX(J).EQ.0.0) LWX=9.9
      IF(DIM2.EQ.'cm') LWX=LWX*10.
      IF(PYMAX(J).EQ.0.0) GO TO 14
      LWY=-R34(J)*YMAX(J)/PYMAX(J)
 14   IF(PYMAX(J).EQ.0.0) LWY=9.9
      IF(DIM2.EQ.'cm') LWY=LWY*10.
C
C   WINKEL CHIX & CHIY
C
      CHIX=180./PI*ASIN(R12(J))
      CHIY=180./PI*ASIN(R34(J))
C
C   DISPERSION HORIZONTAL
C
      DX=D(J)*DELP
      DPX=DP(J)*DELP
C
C   ZEICHNE ELLIPSEN
C
      OFFX1 = OFFX(NJ)
      OFFY1 = OFFY(NJ)
       IF (NNR .EQ. 1) THEN
        OFFX1 = OFFX(3)
        OFFY1 = OFFY(3)
       ENDIF
      OFFX2 = OFFX1+500.
      BEGX1 = OFFX1-200.
      ENDX1 = OFFX1+200.
      BEGX2 = OFFX2-200.
      ENDX2 = OFFX2+200.
      BEGY1 = OFFY1-200.
      ENDY1 = OFFY1+200.
      CALL SETCOL(3) ! GRUEN
      CALL PSMOV((OFFX1+X1(1))*SCALX,(OFFY1+Y1(1))*SCALY)
      DO 15 I=2,101
      CALL PSDRW((OFFX1+X1(I))*SCALX,(OFFY1+Y1(I))*SCALY)
 15   CONTINUE
      CALL PSMOV((OFFX2+X2(1))*SCALX,(OFFY1+Y2(1))*SCALY)
      DO 25 I=2,101
      CALL PSDRW((OFFX2+X2(I))*SCALX,(OFFY1+Y2(I))*SCALY)
 25   CONTINUE
C
C   KONJUGIERTE DURCHMESSER
C
      XK1=R12(J)*X1M
      XK2=R12(J)*Y1M
      XK3=R34(J)*X2M
      XK4=R34(J)*Y2M
      XL1=X1M
      XL2=Y1M
      XL3=X2M
      XL4=Y2M
      CALL PSMOV((OFFX1+XK1)*SCALX,(OFFY1+XL2)*SCALY)
      CALL PSDRW((OFFX1-XK1)*SCALX,(OFFY1-XL2)*SCALY)
      CALL PSMOV((OFFX1+XL1)*SCALX,(OFFY1+XK2)*SCALY)
      CALL PSDRW((OFFX1-XL1)*SCALX,(OFFY1-XK2)*SCALY)
      CALL PSMOV((OFFX2+XK3)*SCALX,(OFFY1+XL4)*SCALY)
      CALL PSDRW((OFFX2-XK3)*SCALX,(OFFY1-XL4)*SCALY)
      CALL PSMOV((OFFX2+XL3)*SCALX,(OFFY1+XK4)*SCALY)
      CALL PSDRW((OFFX2-XL3)*SCALX,(OFFY1-XK4)*SCALY)
C
C   ZEICHNE ACHSEN
C
      CALL SETCOL(2) ! ROT
      CALL PSMOV(BEGX1*SCALX,OFFY1*SCALY)
      CALL PSDRW(ENDX1*SCALX,OFFY1*SCALY)
      CALL PSMOV(OFFX1*SCALX,BEGY1*SCALY)
      CALL PSDRW(OFFX1*SCALX,ENDY1*SCALY)
      CALL PSMOV(BEGX2*SCALX,OFFY1*SCALY)
      CALL PSDRW(ENDX2*SCALX,OFFY1*SCALY)
      CALL PSMOV(OFFX2*SCALX,BEGY1*SCALY)
      CALL PSDRW(OFFX2*SCALX,ENDY1*SCALY)
C
C   ZEICHNE ACHSEN-ABSCHNITTE
C
      DO 300 K=1,10
      XX1=(OFFX1+FSCAL(1)*FLOAT(K))*SCALX
      XX2=(OFFX1-FSCAL(1)*FLOAT(K))*SCALX
      IF(XX1.GT.ENDX1*SCALX) GO TO 300
      IF(XX2.LT.BEGX1*SCALX) GO TO 300
      CALL PSMOV(XX1,(OFFY1-3.)*SCALY)
      CALL PSDRW(XX1,(OFFY1+3.)*SCALY)
      CALL PSMOV(XX2,(OFFY1-3.)*SCALY)
      CALL PSDRW(XX2,(OFFY1+3.)*SCALY)
 300  CONTINUE
      DO 301 K=1,10
      XX1=(OFFX2+FSCAL(2)*FLOAT(K))*SCALX
      XX2=(OFFX2-FSCAL(2)*FLOAT(K))*SCALX
      IF(XX1.GT.ENDX2*SCALX) GO TO 301
      IF(XX2.LT.BEGX2*SCALX) GO TO 301
      CALL PSMOV(XX1,(OFFY1-3.)*SCALY)
      CALL PSDRW(XX1,(OFFY1+3.)*SCALY)
      CALL PSMOV(XX2,(OFFY1-3.)*SCALY)
      CALL PSDRW(XX2,(OFFY1+3.)*SCALY)
 301  CONTINUE
      DO 302 K=1,10
      YY1=(OFFY1+FSCAL(3)*FLOAT(K))*SCALY
      YY2=(OFFY1-FSCAL(3)*FLOAT(K))*SCALY
      IF(YY1.GT.ENDY1*SCALY) GO TO 302
      IF(YY2.LT.BEGY1*SCALY) GO TO 302
      CALL PSMOV((OFFX1-3.)*SCALX,YY1)
      CALL PSDRW((OFFX1+3.)*SCALX,YY1)
      CALL PSMOV((OFFX1-3.)*SCALX,YY2)
      CALL PSDRW((OFFX1+3.)*SCALX,YY2)
 302  CONTINUE
      DO 303 K=1,10
      YY1=(OFFY1+FSCAL(4)*FLOAT(K))*SCALY
      YY2=(OFFY1-FSCAL(4)*FLOAT(K))*SCALY
      IF(YY1.GT.ENDY1*SCALY) GO TO 303
      IF(YY2.LT.BEGY1*SCALY) GO TO 303
      CALL PSMOV((OFFX2-3.)*SCALX,YY1)
      CALL PSDRW((OFFX2+3.)*SCALX,YY1)
      CALL PSMOV((OFFX2-3.)*SCALX,YY2)
      CALL PSDRW((OFFX2+3.)*SCALX,YY2)
 303  CONTINUE
C
C   BESCHRIFTE ACHSEN
C
      CALL PSTEXT((OFFX1+5.)*SCALX,(ENDY1+10.)*SCALY,'X''')
      CALL PSTEXT((OFFX2+5.)*SCALX,(ENDY1+10.)*SCALY,'Y''')
      CALL PSTEXT((ENDX1+5.)*SCALX,OFFY1*SCALY,'X')
      CALL PSTEXT((ENDX2+5.)*SCALX,OFFY1*SCALY,'Y')
C
C   ZEICHNE PARAMETER
C
      CALL SETCOL(1) ! WEISS
      WRITE(TEXT,1004) LWX,DIM1
 1004 FORMAT('LWX=',F7.2,1X,A2)
      CALL PSTEXT(BEGX1*SCALX,(ENDY1+120.)*SCALY,TEXT)
      WRITE(TEXT,1005) LWY,DIM1
 1005 FORMAT('LWY=',F7.2,1X,A2)
      CALL PSTEXT(BEGX2*SCALX,(ENDY1+120.)*SCALY,TEXT)
      WRITE(TEXT,1006) CHIX, R12(J)
 1006 FORMAT('CHX=',F7.1,' Deg',' (',F7.4,')')
      CALL PSTEXT(BEGX1*SCALX,(ENDY1+85.)*SCALY,TEXT)
      WRITE(TEXT,1007) CHIY, R34(J)
 1007 FORMAT('CHY=',F7.1,' Deg',' (',F7.4,')')
      CALL PSTEXT(BEGX2*SCALX,(ENDY1+85.)*SCALY,TEXT)
      WRITE(TEXT,1008) EX, DIM2
 1008 FORMAT('Horz Emit=',F6.2,' * Pi ',A2,'mr')
      CALL PSTEXT(BEGX1*SCALX,(ENDY1+50.)*SCALY,TEXT)
      WRITE(TEXT,1009) EY, DIM2
 1009 FORMAT('Vert Emit=',F6.2,' * Pi ',A2,'mr')
      CALL PSTEXT(BEGX2*SCALX,(ENDY1+50.)*SCALY,TEXT)
      WRITE(TEXT,1013) XSCAL,DIM2,PXSCAL
 1013 FORMAT('Xmax=',F5.1,1X,A2,'  X''max=',F5.1,' mr')
      CALL PSTEXT(BEGX1*SCALX,(BEGY1-30.)*SCALY,TEXT)
      WRITE(TEXT,1012) YSCAL,DIM2,PYSCAL
 1012 FORMAT('Ymax=',F5.1,1X,A2,'  Y''max=',F5.1,' mr')
      CALL PSTEXT(BEGX2*SCALX,(BEGY1-30.)*SCALY,TEXT)
      WRITE(TEXT,1011) DX,DIM2,DPX
 1011 FORMAT('D=   ',F5.1,1X,A2,'  D''=   ',F5.1,' mr')
      CALL PSTEXT(BEGX1*SCALX,(BEGY1-65.)*SCALY,TEXT)
      WRITE(TEXT,1010) Z(J),DIM1,LAB(J)
 1010 FORMAT('z= ',F7.3,1X,A2,2X,A4)
      CALL PSTEXT(BEGX1*SCALX,(BEGY1-100.)*SCALY,TEXT)
C
 97   CONTINUE
      CALL PSCLS
      RETURN
      END
